description = "myTest"

apply plugin: "kotlin"

dependencies {
    compile(project(":spring-core"))
    compile(project(":spring-aop"))
    compile(project(":spring-beans"))
    compile(project(":spring-context"))
    compile(project(":spring-jdbc"))
    compile(project(":spring-orm"))
    compile(project(":spring-tx"))
    compile(project(":spring-web"))
    compile(project(":spring-webflux"))
    compile(project(":spring-webmvc"))
    compile(project(":spring-websocket"))
    compile("junit:junit")
    compile("org.junit.jupiter:junit-jupiter-api")
    compile("org.testng:testng")
    compile("org.aspectj:aspectjweaver")
    compile("org.codehaus.groovy:groovy")
    compile("org.hamcrest:hamcrest")
    compile("org.apache.taglibs:taglibs-standard-jstlel")
    compile("net.sourceforge.htmlunit:htmlunit")
    compile("org.seleniumhq.selenium:htmlunit-driver")
    compile("org.seleniumhq.selenium:selenium-java")
    compile("org.xmlunit:xmlunit-matchers")
    compile("org.skyscreamer:jsonassert")
    compile("com.jayway.jsonpath:json-path")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("org.jetbrains.kotlin:kotlin-stdlib")
    compile("io.projectreactor:reactor-test")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-core")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-reactor")
    testCompile(project(":spring-context-support"))
    testCompile(project(":spring-oxm"))
    testCompile(testFixtures(project(":spring-beans")))
    testCompile(testFixtures(project(":spring-context")))
    testCompile(testFixtures(project(":spring-core")))
    testCompile(testFixtures(project(":spring-tx")))
    testCompile(testFixtures(project(":spring-web")))
    testCompile("javax.annotation:javax.annotation-api")
    testCompile("javax.cache:cache-api")
    testCompile("javax.ejb:javax.ejb-api")
    testCompile("javax.interceptor:javax.interceptor-api")
    testCompile("javax.mail:javax.mail-api")
    testCompile("org.hibernate:hibernate-core")
    testCompile("org.hibernate:hibernate-validator")
    testCompile("javax.validation:validation-api")
    testCompile("org.junit.platform:junit-platform-runner") {
        exclude group: "junit", module: "junit"
    }
    testCompile("org.junit.platform:junit-platform-testkit")
    testCompile("com.fasterxml.jackson.core:jackson-databind")
    testCompile("com.thoughtworks.xstream:xstream")
    testCompile("com.rometools:rome")
    testCompile("org.apache.tiles:tiles-api")
    testCompile("org.apache.tiles:tiles-core")
    testCompile("org.apache.tiles:tiles-servlet")
    testCompile("org.hsqldb:hsqldb")
    testCompile("org.apache.httpcomponents:httpclient")
    testCompile("io.projectreactor.netty:reactor-netty")
    testCompile("de.bechte.junit:junit-hierarchicalcontextrunner")
    testRuntime("org.junit.vintage:junit-vintage-engine") {
        exclude group: "junit", module: "junit"
    }
    testRuntime("org.glassfish:javax.el")
    testRuntime("com.sun.xml.bind:jaxb-core")
    testRuntime("com.sun.xml.bind:jaxb-impl")
}

task junit(type: Test) {
    description = "Runs JUnit 4 and JUnit Jupiter tests."
    useJUnitPlatform {
        excludeTags "failing-test-case"
    }
    include(["**/*Tests.class", "**/*Test.class"])
    exclude(["**/testng/**/*.*"])
    systemProperty("testGroups", project.properties.get("testGroups"))
    // Java Util Logging for the JUnit Platform.
    // systemProperty("java.util.logging.manager", "org.apache.logging.log4j.jul.LogManager")
}

task testNG(type: Test) {
    description = "Runs TestNG tests."
    useTestNG()
    include(["**/testng/**/*Tests.class", "**/testng/**/*Test.class"])
    systemProperty("testGroups", project.properties.get("testGroups"))
    // Show STD_OUT & STD_ERR of the test JVM(s) on the console:
    // testLogging.showStandardStreams = true
    // forkEvery 1
}

test {
    description = "Runs all tests."
    dependsOn junit, testNG
    exclude(["**/*.*"])
}

task aggregateTestReports(type: TestReport) {
    description = "Aggregates JUnit and TestNG test reports."
    destinationDir = test.reports.html.destination
    reportOn junit, testNG
}

check.dependsOn aggregateTestReports
